/*
 * Copyright (c) 2020-2023 Tesla (Yinsen) Zhang.
 * Use of this source code is governed by the MIT license that can be found in the LICENSE.md file.
 */

{
  // only for the compiler, not the IDE
  generate=[
    java='11'
    psi-factory='no'
    visitor='no'
  ]
  generatePsi=false

  // we only need the parser and element types.
  parserClass='org.aya.lamett.parser.LamettPsiParser'
  elementTypeHolderClass='org.aya.lamett.parser.LamettPsiElementTypes'
  elementTypeClass='org.aya.lamett.parser.LamettPsiElementType'
  tokenTypeClass='org.aya.lamett.parser.LamettPsiTokenType'

  tokens = [
    // universe
    KW_ULIFT = 'regexp:ulift|\u2191'
    KW_TYPE = 'Type'
    KW_ISET = 'ISet'

    KW_DEF = 'def'
    KW_CLASS = 'class'
    KW_CLASSIFIYING = 'classifying'
    KW_DATA = 'data'
    KW_EXTENDS = 'extends'
    KW_NEW = 'new'
    KW_SELF = 'self'
    KW_OVERRIDE = 'override'

    // Unimplemented but reserved
    KW_CODATA = 'codata'

    // symbols
    KW_SIGMA =  'regexp:Sig|\u03a3'
    // KW_SIGMA =  'Sig'
    KW_LAMBDA = 'fn'
    KW_PI = 'Fn'
    KW_FORALL = 'regexp:forall|\u2200'
    // KW_FORALL = 'forall'

    // interval algebra

    KW_IFORALL='regexp:iforall|i\u2200'
    KW_IEQ='regexp:ieq|i=|\u2261'

    KW_INEG='regexp:ineg|Â¬'
    KW_DISJ='regexp:\\/|\u2228'
    KW_CONJ='regexp:/\\|\u2227'

    // ---- AyaLexer end: Keywords

    // TO = 'regexp:->|\u2192'
    TO = '->'
    // LARROW = 'regexp:<-|\u2190'
    LARROW = '<-'
    // IMPLIES = 'regexp:=>|\u21d2'
    IMPLIES = '=>'
    DEFINE_AS = ':='
    SUCHTHAT = '**'
    DOT = '.'
    BAR = '|'
    COMMA = ','
    COLON = ':'

    // markers
    LBRACE = '{'
    RBRACE = '}'
    LPAREN = '('
    RPAREN = ')'
    // LIDIOM = 'regexp:\(\||\u2987'
    LIDIOM = '(|'
    // RIDIOM = 'regexp:\|\)|\u2988'
    RIDIOM = '|)'
    LGOAL = '{?'
    RGOAL = '?}'
    // LPARTIAL = 'regexp:\{\||\u2983'
    LPARTIAL = '{|'
    // RPARTIAL = 'regexp:\|}|\u2984'
    RPARTIAL = '|}'
    // LPATH = 'regexp:\[\||\u27E6'
    LPATH = '[|'
    // RPATH = 'regexp:\|]|\u27E7'
    RPATH = '|]'
    AT = '@'

    CALM_FACE = '_'

    // used for bootstrapping lexer
    ID = 'regexp:[a-zA-Z_][a-zA-Z0-9_]*'
    NUMBER = 'regexp:[0-9]+'
  ]

  // IMPORTANT: when editing extends(..), please also modify `AyaGKProducer` accordingly!
  extends("literal") = expr
  extends("refExpr|holeExpr|constExpr") = literal
  extends("goalExpr|calmFaceExpr") = holeExpr
  // Catch-all! Wow!
  extends(".*Expr") = expr

  extends(".+Atom") = atomExpr
  extends(".*Decl") = decl
}

program ::= stmts
private stmts ::= stmt_with_recover*
private stmt_with_recover ::= !(<<eof>>) decl {
  pin=1
  recoverWhile = stmt_recover
}
private stmt_recover ::= !(stmt_first)
private stmt_first ::= DOC_COMMENT | KW_DEF | KW_CLASS | KW_DATA | KW_PRINT

// statements

// declarations

decl ::= fnDecl | classDecl | dataDecl | printDecl

printDecl ::= KW_PRINT tele* type simpleBody

fnDecl ::=
 KW_DEF weakId
 tele* type fnBody

private simpleBody ::= IMPLIES expr { pin=1 }
fnBody ::= simpleBody
         | barredClause*

classDecl
  ::= KW_CLASS weakId (KW_EXTENDS <<commaSep weakId>>)?
      (BAR classMember)*

classMember
  ::= KW_CLASSIFIYING? weakId tele* type IMPLIES expr

dataDecl ::= KW_DATA weakId tele* dataBody*

dataBody ::= BAR (dataCtorClause | dataCtor) {
  pin=1
}

dataCtor ::= weakId tele* type? partialBlock?
partialBlock ::= <<braced partialInner>>

dataCtorClause ::= patterns IMPLIES dataCtor

// expressions
expr ::= newExpr
       | piExpr
       | forallExpr
       | sigmaExpr
       | lambdaExpr
       | selfExpr
       | pathExpr
       | atomExpr
       | arrowExpr
       | appExpr
       | projExpr
       | disjExpr
       | conjExpr
       | iforallExpr
       | ieqExpr
       | inegExpr

atomExpr ::= tupleAtom | partialAtom | literal
tupleAtom ::= LPAREN exprList RPAREN

newExpr ::= KW_NEW expr newBody?
appExpr ::= expr argument+
arrowExpr ::= expr TO expr { rightAssociative = true }
projExpr ::= expr projFix
piExpr ::= KW_PI tele+ TO expr
forallExpr ::= KW_FORALL lambdaTele+ TO expr
sigmaExpr ::= KW_SIGMA tele SUCHTHAT expr
lambdaExpr ::= KW_LAMBDA lambdaTele+ (IMPLIES expr)?
selfExpr ::= KW_SELF (AT weakId)?
pathExpr ::= LPATH weakId+ RPATH expr partialBlock?

// Interval algebra
ieqExpr ::= expr KW_EQ expr
iforallExpr ::= KW_FORALL weakId+ IMPLIES expr
inegExpr ::= KW_INEG expr
conjExpr ::= expr KW_CONJ expr
disjExpr ::= expr KW_DISJ expr

partialAtom ::= LPARTIAL partialInner? RPARTIAL
private partialInner ::= bareSubSystem? barredSubSystem*
subSystem ::= expr DEFINE_AS expr
bareSubSystem ::= subSystem
barredSubSystem ::= BAR subSystem

newArgField ::= weakId
newArg ::= BAR newArgField weakId* IMPLIES expr
// New body new body but you!
newBody ::= <<braced newArg*>>

argument ::= atomExpr projFix*

projFix ::= DOT (NUMBER | projFixId)
// kiva: Used in IntelliJ IDEA
projFixId ::= weakId

// barredClause and bareClause made inspection quick fix easier
// see: DominationInspection
barredClause ::= BAR clause
clause ::= <<commaSep patterns>> (IMPLIES expr)?

patterns ::= pattern+
pattern ::= <<paren patterns>>
              | LPAREN RPAREN
              | weakId

literal ::= refExpr | holeExpr | constExpr
holeExpr ::= goalExpr | calmFaceExpr
calmFaceExpr ::= CALM_FACE

goalExpr ::= LGOAL expr? RGOAL
refExpr ::= weakId
constExpr ::= KW_TYPE | KW_ISET | KW_SET | KW_INTERVAL | NUMBER | KW_F

// tele is always typed
tele ::= literal | <<paren teleBinder>>

// Explicit arguments may be anonymous
teleBinder ::= teleBinderTyped
             | teleBinderAnonymous
teleBinderUntyped ::= weakId+
teleBinderTyped ::= teleBinderUntyped type
teleBinderAnonymous ::= expr

// lambda tele is always named
lambdaTele ::= weakId | <<paren lambdaTeleBinder>>

lambdaTeleBinder ::= teleBinderTyped
                   | teleBinderUntyped

// utilities
private exprList ::= <<commaSep expr>>
type ::= COLON expr { pin=1 }
weakId ::= ID

// meta rules

// we can improve the parsing by producing generically from a 'commaSep' rule
meta commaSep ::= <<param>> (',' <<param>>) *
meta paren ::= LPAREN <<param>> RPAREN
private meta braced ::= LBRACE <<param>> RBRACE
